# courtesy of Phil Gregory

autoload -U git_prompt

function precmd {
    local TERMWIDTH
    (( TERMWIDTH = ${COLUMNS} - 1 ))

    # Truncate the path if it's too long.
    PR_FILLBAR=""
    PR_PWDLEN=""
    
    GITBRANCH="$(git_prompt)"
    if [ -n "$GITBRANCH" ]; then
        GITBRANCH=" ($GITBRANCH)"
    fi
    GITBRANCH=$GITBRANCH

    local promptsize=${#${(%):---(%n@%m:%l)---()--}}
    local pwdsize=$((${#GITBRANCH}+${#${(%):-%~}})) # remove GIT's branch and PWD
    #local pwdsize=${#${(%):-%~}}

    if [[ "$promptsize + $pwdsize" -gt $TERMWIDTH ]]; then
    ((PR_PWDLEN=$TERMWIDTH - $promptsize))
    else
    PR_FILLBAR="\${(l.(($TERMWIDTH - ($promptsize + $pwdsize)))..${PR_HBAR}.)}"
    fi
    
}

setopt extended_glob
preexec () {
    if [[ "$TERM" == "screen" ]]; then
      local CMD=${1[(wr)^(*=*|sudo|-*)]}
      echo -ne "\ek$CMD\e\\"
    fi
}

setprompt () {
    setopt prompt_subst
    autoload pr_colors extended_chars

    pr_colors
    extended_chars

    case $TERM in
    xterm*)
        PR_TITLEBAR=$'%{\e]0;%(!.-=*[ROOT]*=- | .)%n@%m:%~ | ${COLUMNS}x${LINES} | %y\a%}'
    ;;
    rxvt*)
        PR_TITLEBAR=$'%{\e]2;%(!.-=*[ROOT]*=- | .)%n@%m:%~ | %y\a%}'
    ;;
    urxvt*)
        PR_TITLEBAR=$'%{\e]2;%(!.-=*[ROOT]*=- | .)%n@%m:%~ | %y\a%}'
    ;;
    screen)
        PR_TITLEBAR=$'%{\e_screen \005 (\005t) | %(!.-=[ROOT]=- | .)%n@%m:%~ | ${COLUMNS}x${LINES} | %y\e\\%}'
    ;;
    *)
        PR_TITLEBAR=''
    ;;
    esac

    ###
    # Decide whether to set a screen title
    if [[ "$TERM" == "screen" ]]; then
  	  PR_STITLE=$'%{\ekzsh\e\\%}'
    else
      PR_STITLE=''
    fi

    ###
    # ACPI detection

    # if [[ -e /proc/acpi ]]; then
    #     PR_ACPI_RESULT=`acpi -b`
	# 	if acpi | grep "charged" == true; then 
	# 	elif acpi | grep "discharging" == true; then
	# 		acpi | `awk {'print $4'}` | xargs cut -b ","
	# 	fi
    #     PR_ACPI='$PR_RED${PR_ACPI_RESULT[(w)5,(w)6]/\% /%%}$PR_LIGHT_BLUE:'
    # else
    #     PR_ACPI=''
    # fi

    PROMPT='$PR_SET_CHARSET$PR_STITLE${(e)PR_TITLEBAR}\
$PR_CYAN$PR_SHIFT_IN$PR_ULCORNER$PR_BLUE$PR_HBAR$PR_SHIFT_OUT(\
$PR_GREEN%(!.%SROOT%s.%n)$PR_GREEN@%m:%l\
$PR_BLUE)$PR_SHIFT_IN$PR_HBAR$PR_CYAN$PR_HBAR${(e)PR_FILLBAR}$PR_BLUE$PR_HBAR$PR_SHIFT_OUT(\
$PR_MAGENTA%$PR_PWDLEN<...<%~$GITBRANCH%<<\
$PR_BLUE)$PR_SHIFT_IN$PR_HBAR$PR_CYAN$PR_URCORNER$PR_SHIFT_OUT\

$PR_CYAN$PR_SHIFT_IN$PR_LLCORNER$PR_BLUE$PR_HBAR$PR_SHIFT_OUT(\
%(?..$PR_LIGHT_RED%?$PR_BLUE:)\
$PR_YELLOW%T\
$PR_LIGHT_BLUE:%(!.$PR_RED.$PR_WHITE)%#$PR_BLUE)$PR_SHIFT_IN$PR_HBAR$PR_SHIFT_OUT\
$PR_CYAN$PR_SHIFT_IN$PR_HBAR$PR_SHIFT_OUT\
$PR_NO_COLOUR '

RPROMPT=' $PR_CYAN$PR_SHIFT_IN$PR_HBAR$PR_BLUE$PR_HBAR$PR_SHIFT_OUT\
($PR_YELLOW%D{%a,%b%d}$PR_BLUE)$PR_SHIFT_IN$PR_HBAR$PR_CYAN$PR_LRCORNER$PR_SHIFT_OUT$PR_NO_COLOUR'


PS2='$PR_CYAN$PR_SHIFT_IN$PR_HBAR$PR_SHIFT_OUT\
$PR_BLUE$PR_SHIFT_IN$PR_HBAR$PR_SHIFT_OUT(\
$PR_LIGHT_GREEN%_$PR_BLUE)$PR_SHIFT_IN$PR_HBAR$PR_SHIFT_OUT\
$PR_CYAN$PR_SHIFT_IN$PR_HBAR$PR_SHIFT_OUT$PR_NO_COLOUR '

}

